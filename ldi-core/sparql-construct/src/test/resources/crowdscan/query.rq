PREFIX rdf:          <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd:          <http://www.w3.org/2001/XMLSchema#>
PREFIX schema:       <http://schema.org/>
PREFIX time:         <http://www.w3.org/2006/time#>
PREFIX cs-count:     <http://www.crowdscan.be/ns/count#>

CONSTRUCT {
  GRAPH ?observation {
    ?observation rdf:type cs-count:Observation .
    ?observation cs-count:time ?time .
    ?observation cs-count:value ?value .
    ?observation cs-count:environment ?environment .
    ?observation cs-count:timedelta ?delta .
    ?observation cs-count:region ?zone .
  }
} WHERE {
    ?header cs-count:environment ?environment .
    ?header cs-count:time ?time .
    ?header cs-count:timedelta ?timedelta .
    bind(strdt(concat("-PT", str(?timedelta), "M"), xsd:duration) as ?delta) .

    ?payload cs-count:payload ?regions .
    {
      SELECT ?value (count(?mid) as ?zone)
      WHERE
      {
        ?regions rdf:rest* ?mid .
        ?mid rdf:rest ?node .
        ?node rdf:first ?value .
      }
      GROUP BY ?node ?value
    }
    bind(uri(concat("http://www.crowdscan.be/id/observation/", ?environment, "/", str(?zone))) as ?observation) .
  }