PREFIX rdf:             <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dcterms:         <http://purl.org/dc/terms/>
PREFIX miv:             <http://www.wegenenverkeer.be/ns/verkeersmetingen#>
PREFIX xsd:             <http://www.w3.org/2001/XMLSchema#>

PREFIX dv-vm:           <https://data.vlaanderen.be/ns/verkeersmetingen#>
PREFIX iso19156:        <http://def.isotc211.org/iso19156/2011/>
PREFIX iso19156-co:     <http://def.isotc211.org/iso19156/2011/CountObservation#>
PREFIX iso19156-om-co:  <http://def.isotc211.org/iso19156/2011/CountObservation#OM_CountObservation.>
PREFIX iso19156-om-ob:  <http://def.isotc211.org/iso19156/2011/Observation#OM_Observation.>
PREFIX time:            <http://www.w3.org/2006/time#>
PREFIX sosa:            <http://www.w3.org/ns/sosa/>
PREFIX ssn:             <http://www.w3.org/ns/ssn/>
PREFIX prov:            <http://www.w3.org/ns/prov#>

CONSTRUCT {
  {
    ?create_id a time:Instant ; time:inXSDDateTimeStamp ?tijd_waarneming  .
    ?change_id a time:Instant ; time:inXSDDateTimeStamp ?tijd_laatst_gewijzigd  .
    ?interval_id a time:ProperInterval ; time:hasBeginning ?create_id ; time:hasEnd ?change_id .
  }

  GRAPH ?count_id {
    ?aantal_id a dv-vm:Verkeerskenmerk ; dv-vm:type "aantal"^^dv-vm:Verkeerskenmerktype ; dv-vm:voertuigType ?klasse .

    ?count_id a dv-vm:Verkeersmeting, iso19156-co:OM_CountObservation ;
      iso19156-om-ob:phenomenonTime ?interval_id ;
      dv-vm:geobserveerdKenmerk ?aantal_id ; iso19156-om-ob:observedProperty ?aantal_id ;
      dv-vm:geobserveerdObject ?rijstrook_id ; iso19156-om-ob:featureOfInterest ?rijstrook_id ;
      iso19156-om-ob:resultTime ?create_id ;
      iso19156-om-ob:result ?verkeersintensiteit ; iso19156-om-co:result ?verkeersintensiteit ;
      dv-vm:verkeersmeetpunt ?meetpunt_id .
  }

  GRAPH ?speed_id {
    ?snelheid_id a dv-vm:Verkeerskenmerk ; dv-vm:type "snelheid"^^dv-vm:Verkeerskenmerktype ; dv-vm:voertuigType ?klasse .

    ?speed_id a dv-vm:Verkeersmeting ;
      prov:generatedAtTime ?tijd_waarneming ;
      iso19156-om-ob:phenomenonTime ?interval_id ;
      dv-vm:geobserveerdKenmerk ?snelheid_id ;
      dv-vm:geobserveerdObject ?rijstrook_id ;
      iso19156-om-ob:resultTime ?create_id ;
      iso19156-om-ob:result ?voertuigsnelheid_rekenkundig ;
      dv-vm:verkeersmeetpunt ?meetpunt_id .
  }
#  ?collection_id a dv-vm:Verkeersmetingverzameling ;
#    prov:generatedAtTime ?tijd_waarneming ;
#    dv-vm:fenomeentijd ?create_id ;
#    dv-vm:uitgevoerdMet ?sensor_id .
#
#  ?sensor_id a sosa:Sensor ;
#    dcterms:type "Verkeersmeting"^^dv-vm:Meetinstrumenttype ;
#    ssn:implements [ a sosa:Procedure ; dcterms:type "???"^^dv-vm:Observatieproceduretype ] .
#
#  ?collection_id dv-vm:heeftLid ?count_id .
#  ?collection_id dv-vm:heeftLid ?speed_id .
}
WHERE {
    ?sub rdf:type <http://www.wegenenverkeer.be/ns/verkeersmetingen#Meting> .
    ?sub miv:unieke_id ?unieke_id .
    ?sub miv:tijd_waarneming ?tijd_waarneming .
    ?sub miv:tijd_laatst_gewijzigd ?tijd_laatst_gewijzigd .
    ?sub miv:meetData ?meetData .
    ?meetData miv:klasse_id ?klasse_id .
    ?meetData miv:verkeersintensiteit ?verkeersintensiteit .
    ?meetData miv:voertuigsnelheid_rekenkundig ?voertuigsnelheid_rekenkundig .

    bind(concat("https://data.vlaanderen.be/id/verkeersmetingen/Verkeersmeting/", str(?unieke_id)) as ?collection) .
#    bind(uri(?collection) as ?collection_id) .

    bind(concat(?collection, "#", str(?tijd_waarneming)) as ?create) .
    bind(uri(?create) as ?create_id) .

    bind(concat(?collection, "#", str(?tijd_laatst_gewijzigd)) as ?change) .
    bind(uri(?change) as ?change_id) .

    bind(concat(?create, "-", str(?tijd_laatst_gewijzigd)) as ?interval) .
    bind(uri(?interval) as ?interval_id) .

    bind(uri(concat("https://data.vlaanderen.be/id/verkeersmetingen/Verkeerskenmerk/aantal/", str(?klasse_id))) as ?aantal_id) .
    bind(strdt(str(?klasse_id), dv-vm:Voertuigtype) as ?klasse) .
    bind(uri(concat("https://data.vlaanderen.be/id/verkeersmetingen/Verkeerskenmerk/snelheid/", str(?klasse_id))) as ?snelheid_id) .

    bind(uri(concat(?collection, "/aantal/", str(?klasse_id))) as ?count_id) .
    bind(uri(concat(?collection, "/snelheid/", str(?klasse_id))) as ?speed_id) .

    bind(concat("https://data.vlaanderen.be/id/verkeersmetingen/Verkeersmeetpunt/", str(?unieke_id)) as ?meetpunt) .
    bind(uri(?meetpunt) as ?meetpunt_id) .
    bind(uri(concat(?meetpunt, "#rijstrook")) as ?rijstrook_id) .

#    bind(concat(?collection, "#sensor") as ?sensor) .
#    bind(uri(?sensor) as ?sensor_id) .

}